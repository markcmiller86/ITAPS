dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.59)
AC_INIT(ITAPS swapping service, 1.2.0, cfog@mech.ubc.ca)
AC_CONFIG_SRCDIR([ITAPS_Swap3D.cc])
AC_CONFIG_HEADER([ITAPS_Swap_config.hh])

AC_CONFIG_AUX_DIR(conf)
AC_CANONICAL_HOST

AC_ARG_ENABLE([tets-from-faces],
AS_HELP_STRING([--enable-tets-from-faces],[build tets from faces, not directly from vertices]))

if (test "X$enable_tets_from_faces" = "Xyes") ; then
  CPPFLAGS="$CPPFLAGS -DCREATE_FROM_FACES"
fi
AC_SUBST(CPPFLAGS)

dnl These are needed by later function calls
AC_PROG_EGREP

dnl Setup IMESH_DEFS variable
AC_ARG_VAR(IMESH_DEFS, [Full path-qualified name of iMesh-Defs.inc] )
if test -z "$IMESH_DEFS"; then
    AC_MSG_ERROR(IMESH_DEFS variable not specified)
else
    if ! test -r $IMESH_DEFS; then
        AC_MSG_ERROR([The file specified for IMESH_DEFS, "$IMESH_DEFS", either does not exist or is not readable])
    fi
fi

dnl Must always test for a C compiler
AC_PROG_CPP
AC_PROG_CC
AC_PROG_CC_C_O

dnl ************************************************************
dnl
dnl Checks for header files, typedefs, structures, and compiler 
dnl characteristics..   Some implementations may need more 
dnl than these; none will need less, as the auto-client code needs 
dnl this much 

AC_HEADER_STDC
AC_C_CONST
AC_C_INLINE

AC_PROG_CXXCPP
AC_PROG_CXX

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

ITAPS_PROG_BUILD_LIB

dnl Miscellaneous checks.
dnl These should really be in ITAPS_PROG_CHECKS...
AC_HEADER_STDBOOL
AC_C_CONST
AC_TYPE_SIZE_T

AC_CHECK_LIB(m, sqrt)
AC_CHECK_FUNCS([pow sqrt])

AC_ARG_VAR([MESQUITE_DIR],[Full path to Mesquite top-level directory])
if test -z "$MESQUITE_DIR"; then
   AC_MSG_WARN([Not enabling support for Mesquite quality metrics.])
else
   if ! test -d $MESQUITE_DIR; then
     AC_MSG_WARN([The specified Mesquite path is not a valid directory.])
   else
     CPPFLAGS="$CPPFLAGS -I$MESQUITE_DIR/include"
     LDFLAGS="$LDFLAGS -L$MESQUITE_DIR/lib"
     AC_LANG_PUSH(C++)
     AC_CHECK_HEADERS(Mesquite.hpp)
     AC_CHECK_LIB([mesquite], [msq_sigint_handler])
     AC_LANG_POP(C++)
   fi
fi 

AC_CONFIG_FILES(Makefile)

AC_OUTPUT
